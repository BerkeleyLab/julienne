! Copyrigh (c) 2024-2025, The Regents of the University of California and Sourcery Institute
! Terms of use are as specified in LICENSE.txt

#include "assert_macros.h"

submodule(julienne_test_suite_m) julienne_test_suite_s
  use assert_m
  use julienne_string_m, only : operator(.csv.)
  implicit none

  character(len=*), parameter :: test_suite_key = "test suite"
  character(len=*), parameter :: modules_key = "modules" 
  character(len=*), parameter :: types_key = "types" 

contains

  module procedure from_components
    test_suite%modules_ = modules
    test_suite%types_ = types
  end procedure

  module procedure from_file
    integer l
    logical test_suite_key_found

    test_suite_key_found = .false.

    associate(lines => file%lines())
      do l=1,size(lines)
        if (lines(l)%get_json_key() == test_suite_key) then
          test_suite_key_found = .true.
          test_suite%modules_= lines(l+1)%get_json_value(string_t(modules_key), mold=[string_t("")])
          test_suite%types_  = lines(l+2)%get_json_value(string_t(types_key), mold=[string_t("")])
          return
        end if
      end do
    end associate

    call_assert(test_suite_key_found)
  end procedure

  module procedure to_file
    character(len=*), parameter :: indent = repeat(" ",ncopies=4)

    file = file_t([  &
       string_t("{") &
      ,string_t(indent // '"' // test_suite_key//  '": {') & 
      ,         indent // indent // '"modules" : [' // .csv. self%modules_%bracket('"')  // '],' &   
      ,         indent // indent // '  "types" : [' // .csv. self%types_%bracket('"')    // ']' &   
      ,string_t(indent // '}') & 
      ,string_t("}") &
    ])  
  end procedure

  module procedure driver_file
    integer i

    file = file_t([ &
       string_t(  "! Copyright (c) 2024-2025, The Regents of the University of California and Sourcery Institute") &
      ,string_t(  "! Terms of use are as specified in LICENSE.txt") // new_line('') &
      ,string_t(  "! DO NOT MODIFY -- this file is generated by Julienne's app/generate-driver.f90") // new_line('') &
      ,string_t(  "program test_suite_driver")                                      &
      ,string_t(  "  use julienne_m, only : test_fixture_t, test_harness_t")        &
      ,[(string_t("  use ") // self%modules_(i)%string(), i=1,size(self%modules_))] &
      ,string_t(  "  implicit none") // new_line('')                                &
      ,string_t(  "  associate(test_harness => test_harness_t([ &"               )  &
      ,[(string_t("     test_fixture_t(") // self%types_(1)%string() // string_t("()) &"))] &
      ,[(string_t("    ,test_fixture_t(") // self%types_(i)%string() // string_t("()) &"), i=2,size(self%types_))]  &
      ,string_t(  "  ]))"                                                        )  &
      ,string_t(  "    call test_harness%report_results"                         )  &
      ,string_t(  "  end associate"                                              )  &
      ,string_t(  "end program test_suite_driver")                                  &
    ])  
  end procedure

end submodule julienne_test_suite_s
